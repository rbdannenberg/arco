# pwe.srp -- piece-wise linear envelope
#
# Roger B. Dannenberg
# May 2023

class Pwe (Ugen):
    def init(points):
        super.init(create_ugen_id(), "Pwe", 1, 'a', "", omit_chans = true)
        set_point_array(points)

    def set_points(rest points):
        set_point_array(points)
        this

    def set_point_array(points):
    # define the shape of the envelope with an array of floats, 
    # d0 y0 d1 y1 ... where di is duration (secs) and yi is amplitude.
    # If the length is odd, a final yn = 0.0 is appended.
        o2_send_start()
        o2_add_int32(arco_ugen_id(id))
        # track cummulative time and samples to avoid accumulated time error
        var time = 0
        var count = 0
        for i = 0 to len(points) by 2:  // process points pair-wise
            time = time + points[i]
            var samps = round(time * AR - count)
            count = count + samps
            o2_add_float(samps)  // the number of samples to ramp
            if i + 1 < len(points):  // allowed to omit final zero amplitude
                o2_add_float(points[i + 1])  // the destination amplitude
        o2_send_finish(0, "/arco/pwe/env", true)
        this

    def start():
        o2_send_cmd("/arco/pwe/start", 0, "U", id)
        this

    def stop():
        o2_send_cmd("/arco/pwe/stop", 0, "U", id)
        this

    def decay(dur):
    # end envelope with decay to zero over given duration (seconds)
        o2_send_cmd("/arco/pwe/decay", 0, "Uf", id, dur * AR)
        this

    def set(y):
    # set current value to y
        o2_send_cmd("/arco/pwe/set", 0, "Uf", id, y)
        this


def pwe(keyword init, start = true, rest points):
    var pwe = Pwe(points)
    if init:
        pwe.set(init)
    if start:
        pwe.start()
    return pwe



class Pweb (Ugen):

    def init(points):
        super.init(create_ugen_id(), "Pweb", 1, 'b', "", omit_chans = true)
        set_point_array(points)
        this

    def set_points(rest points):
        set_point_array(points)
        this

    def set_point_array(points):
    # define the shape of the envelope with an array of floats, 
    # d0 y0 d1 y1 ... where di is duration (secs) and yi is amplitude.
    # If the length is odd, a final yn = 0.0 is appended. Times are
    # converted to block periods (seconds * block_rate).
        // display "Pweb", points
        o2_send_start()
        o2_add_int32(arco_ugen_id(id))
        # track cummulative time and samples to avoid accumulated time error
        var time = 0   // accumulate the total duration (d0 + d1 + ...) here
        var count = 0  // accumulate the integer block counts here
        // print "               ";
        for i = 0 to len(points) by 2:  // process points pair-wise
            time = time + points[i]
	    // Avoid accumulated rounding error from converting durations to
	    // block counts as follows:
	    //   count is the block count where this segment begins
	    //   time * BR is the exact fractional block count where it ends
	    //   samps is therefore the duration we want with compensation
	    //   for any accumulated rounding error.
            var samps = round(time * BR - count)
            count = count + samps
            // print samps,
            o2_add_float(samps)  // the number of samples to ramp
            if i + 1 < len(points):  // allowed to omit final zero amplitude
                // print points[i + 1],
                o2_add_float(points[i + 1])  // the destination amplitude
        // print
        o2_send_finish(0, "/arco/pweb/env", true)
        this


    def start():
        o2_send_cmd("/arco/pweb/start", 0, "U", id)
        this

    def stop():
        o2_send_cmd("/arco/pweb/stop", 0, "U", id)
        this

    def decay(dur):
    # end envelope with decay to zero over given duration (seconds)
        o2_send_cmd("/arco/pweb/decay", 0, "Uf", id, dur * BR)
        this


    def set(y):
    # set current value to y
        o2_send_cmd("/arco/pweb/set", 0, "Uf", id, y)
        this


def pweb(keyword init, start = true, rest points):
    var pweb = Pweb(points)
    if init:
        pweb.set(init)
    if start:
        pweb.start()
    return pweb
