# zitarev.ugen -- Reverb Unit Generator
#
# Vivek Mohan
# September 2024

zitarev(input: 2a, wet: b, gain: b, rt60: b): 2a

FAUST

declare name "zitarev";
declare description "Stereo Reverb Unit Generator for Arco";

import("stdfaust.lib");

zitarev = _,_ <: re.zita_rev1_stereo(rdel,f1,f2,t60dc,t60m,fsmax),_,_ : 
    out_eq,_,_ : dry_wet : out_level
with{
    fsmax = 48000.0; 
    rdel = 60;    
    f1 = 200;
    t60m = rt60;
    t60dc = rt60;
    f2 = 6000;
    out_eq = pareq_stereo(eq1f,eq1l,eq1q) : pareq_stereo(eq2f,eq2l,eq2q);
    pareq_stereo(eqf,eql,Q) = fi.peak_eq_rm(eql,eqf,tpbt), fi.peak_eq_rm(eql,eqf,tpbt)
    with {
        tpbt = wcT/sqrt(max(0,g)); 
        wcT = 2*ma.PI*eqf/ma.SR;  
        g = ba.db2linear(eql); 
    };
    eq1f = 315;
    eq1l = 0;
    eq1q = 3;
    eq2f = 1500;
    eq2l = 0;
    eq2q = 3;
    dry_wet(x,y) = *(dry) + wetsmooth * x, *(dry) + wetsmooth * y 
    with {
        dry = 1.0-wetsmooth;
    };
    wetsmooth = wet : si.smoo;
    gainsmooth = gain : si.smoo;
    out_level = *(gainsmooth),*(gainsmooth);
};

process(left, right, wet, gain, rt60) = left,right:zitarev;
